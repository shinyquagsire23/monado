// Copyright 2020-2022, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

plugins {
    id 'com.android.library'
    id 'kotlin-android'

    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'

    id 'com.diffplug.spotless'
}

android {
    compileSdk project.sharedCompileSdk
    buildToolsVersion buildToolsVersion


    defaultConfig {
        // needed for ASharedMemory, etc.
        minSdkVersion 26
        targetSdkVersion project.sharedTargetSdk

        // Single point of truth for these names.
        def serviceAction = "org.freedesktop.monado.ipc.CONNECT"
        def shutdownAction = "org.freedesktop.monado.ipc.SHUTDOWN"
        manifestPlaceholders = [
                serviceActionName : serviceAction,
                shutdownActionName: shutdownAction
        ]
        buildConfigField("String", "SERVICE_ACTION", "\"${serviceAction}\"")
        buildConfigField("String", "SHUTDOWN_ACTION", "\"${shutdownAction}\"")
        buildConfigField("Long", "WATCHDOG_TIMEOUT_MILLISECONDS", "1500L")
    }

    buildTypes {
        release {
            minifyEnabled false
            // Gradle plugin produces proguard-android-optimize.txt from @Keep annotations
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.kotlin_module']
        }
    }
    namespace 'org.freedesktop.monado.ipc'
    lint {
        fatal 'StopShip'
    }
}

dependencies {
    implementation project(':src:xrt:auxiliary:android')
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // for Hilt dependency injection
    api "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
}

spotless {
    java {
        target 'src/main/java/**/*.java'
        // apply a specific flavor of google-java-format.
        googleJavaFormat('1.8').aosp().reflowLongStrings()
        // fix formatting of type annotations.
        formatAnnotations()
    }

    kotlin {
        target 'src/main/java/**/*.kt'
        // Use ktfmt(https://github.com/facebook/ktfmt) as the default Kotlin formatter.
        ktfmt('0.43').kotlinlangStyle()
    }
}
