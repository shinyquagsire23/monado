// Copyright 2020, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

plugins {
    id 'com.android.library'
    id 'kotlin-android'

    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion project.sharedTargetSdk
    buildToolsVersion '30.0.2'


    defaultConfig {
        // needed for ASharedMemory, etc.
        minSdkVersion 26
        targetSdkVersion project.sharedTargetSdk

        // Single point of truth for these names.
        def serviceAction = "org.freedesktop.monado.ipc.CONNECT"
        def shutdownAction = "org.freedesktop.monado.ipc.SHUTDOWN"
        manifestPlaceholders = [
                serviceActionName : serviceAction,
                shutdownActionName: shutdownAction
        ]
        buildConfigField("String", "SERVICE_ACTION", "\"${serviceAction}\"")
        buildConfigField("String", "SHUTDOWN_ACTION", "\"${shutdownAction}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            // Gradle plugin produces proguard-android-optimize.txt from @Keep annotations
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        fatal 'StopShip'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation project(':src:xrt:auxiliary:android')
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // for Hilt dependency injection
    api "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
}
